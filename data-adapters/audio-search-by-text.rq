PREFIX ac: <http://audiocommons.org/ns/audiocommons#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX iter: <http://w3id.org/sparql-generate/iter/>
PREFIX fn: <http://w3id.org/sparql-generate/fn/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
GENERATE {
  ?audioClip
      rdf:type ac:AudioClip ;
      dc:title ?title ;
      ac:available_as ?audioFile ;
      rdf:type prov:Entity ;
      prov:wasAttributedTo <http://europeana.eu> .

  ?audioFile rdf:type ac:AudioFile .

  ?audioCollection
      rdf:type ac:AudioCollection, prov:Entity ;
      prov:wasAttributedTo <http://europeana.eu> ;
      ac:memberNode ?audioCollectionNode ;
      ac:nodeCount ?nodeCount .

  ?audioCollectionNode
      a ac:AudioCollectionNode ;
      ac:nodeIndex ?index ;
      ac:nodeContent ?audioClip .

  <http://europeana.eu>
      rdf:type prov:Agent, foaf:Organization ;
      foaf:name "Europeana" .

}
SOURCE <https://www.europeana.eu/api/v2/search.json?wskey=$token&query=$pattern> AS ?source
ITERATOR iter:JSONElement(?source,"$.results[*]") AS ?resIterator
WHERE {
  {	SELECT ?audioCollection WHERE {
      BIND(BNODE() AS ?audioCollection)
    }
  }
  BIND(BNODE() AS ?audioCollectionNode)
  BIND(fn:JSONPath(?resIterator, "element") AS ?res)
  BIND(fn:JSONPath(?resIterator, "position") AS ?indexFromZero)
  BIND(?indexFromZero + 1 AS ?index)

  BIND(IRI(fn:JSONPath(?res, "link")) AS ?audioClip)
  BIND(fn:JSONPath(?res, "type") AS ?type)
  BIND(fn:JSONPath(?res, "title[0]") AS ?title)
  BIND(IRI(fn:JSONPath(?res, "edmIsShownBy[0]")) AS ?audioFile)
  FILTER(?type = "SOUND")
}
