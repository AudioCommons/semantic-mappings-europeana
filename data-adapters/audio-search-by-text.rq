PREFIX ac: <http://audiocommons.org/ns/audiocommons#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX iter: <http://w3id.org/sparql-generate/iter/>
PREFIX fn: <http://w3id.org/sparql-generate/fn/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>                
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
GENERATE { 
  ?audioClip rdf:type ac:AudioClip .
  ?audioClip ac:available_as ?audioFile .
  ?audioClip dc:title ?title .
  ?audioFile rdf:type ac:AudioFile .
  ?audioClip rdf:type prov:Entity .
  <http://europeana.eu> rdf:type prov:Agent .
  <http://europeana.eu> rdf:type foaf:Organization .
  <http://europeana.eu> foaf:name "Europeana" .
  ?audioClip prov:wasAttributedTo <http://europeana.eu> .
}
SOURCE <https://www.europeana.eu/api/v2/search.json?wskey=$token&query=$pattern> AS ?source
ITERATOR iter:JSONPath(?source,"$..items[*]") AS ?res
WHERE {
  BIND(IRI(fn:JSONPath(?res, "link")) AS ?audioClip)
  BIND(fn:JSONPath(?res, "type") AS ?type)
  BIND(fn:JSONPath(?res, "title[0]") AS ?title)  
  BIND(IRI(fn:JSONPath(?res, "edmIsShownBy[0]")) AS ?audioFile)
  FILTER(?type = "SOUND")
}